10  POKE 2,173: POKE 3,48: POKE 4,192: POKE 5,136: POKE 6,208: POKE 7,4: POKE 8,198: POKE 9,1: POKE 10,240
20  POKE 11,8: POKE 12,202: POKE 13,208: POKE 14,246: POKE 15,166: POKE 16,0: POKE 17,76: POKE 18,2: POKE 19,0: POKE 20,96
30 DIM A$(20)
40 TEXT :CALL -936
50 VTAB 10
60 TAB 14:PRINT "PEDESTRIAN"
70 PRINT :TAB 18:PRINT "BY"
80 PRINT :TAB 13:PRINT "JENNY SCHMIDT"
90 FOR W=0 TO 3000:NEXT W
95 PRINT :PRINT :PRINT 
100 TAB 14:PRINT "INSTUCTIONS"
110 PRINT " YOU, THE PEDESTRIAN, HAVE UNWITTINGLY":PRINT "WALKED ONTO MAIN STREET DURING THE RUSH":PRINT "HOUR";
120 PRINT ", AND BELIEVING IN PRESERVATION OF":PRINT "LIFE, (ESPECIALLY YOUR OWN) YOU TRY TO  DODGE THE TRAFFIC."
130 PRINT :PRINT " TRAFFIC DODGING IS DONE WITH THE ZERO  PADDLE. TURNING THE DIAL LEFT MOVES YOU LEFT. TURNING THE DIAL ";
140 PRINT "RIGHT MOVES YOU  RIGHT. TURNING THE DIAL TO THE MIDDLE   MAKES YOU MOVE IN A STRAIGHT PATH."
150 PRINT :PRINT "THE CAR LOOKS LIKE THIS   #####":FOR E=1 TO 3:TAB 29:PRINT "#":NEXT E:PRINT "YOU LOOK LIKE THIS  #     #####"
160 TAB 20:PRINT "###":TAB 21:PRINT "#":PRINT "PRESS ANY KEY";:TAB 20:PRINT "# #":CALL -756
170 PRINT " IF YOU ARE IN A DIRE SITUATION YOU MAY RUN BY USING THE PUSH BUTTON. (HOWEVER, YOU HAVE TO STOP AND CATCH YOUR";
180 PRINT " BREATH  WHILE THE CAR IS STILL GOING!!!!!!)<CTRL-J>"
190 PRINT "YOU CAN PLAY AT FOUR DIFFERENT SKILLS"
200 PRINT :PRINT :PRINT "/1/ BEGINNER (3 CARS)":PRINT :PRINT "/2/ NOVICE (6 CARS)":PRINT :PRINT "/3/ SEMI-PRO (9 CARS)"
210 PRINT :PRINT "/4/ PRO (12 CARS)":PRINT :PRINT :PRINT 
220 PRINT "IF YOUR MOTHER DOESN'T LET YOU CROSS    STREETS ALONE YET, YOU HAD BEST CHOOSE  BEGINNER."
230 PRINT :PRINT :INPUT "WHICH SKILL LEVEL (1-4)",S:IF S>4 OR S<1 THEN 230
240 CALL -936
250 L=S:REM L=SKILL LEVEL S=LAPS X AND Y MAKE MAN A AND B MAKE THE CAR
260 S=3*S+1:GR :Y=35
270 REM  STOP SIGN
280 M=6:N=11:COLOR= 1:FOR P=28 TO 31:M=M-1:N=N+1:VLIN M,N AT P:NEXT P
290 FOR P=32 TO 36:VLIN M,N AT P:NEXT P:COLOR= 5:VLIN N+1,39 AT 33:VLIN N+1,39 AT 34:VLIN 38,39 AT 32:VLIN 38,39 AT 35:COLOR= 1
300 FOR P=37 TO 39:M=M+1:N=N-1:VLIN M,N AT P:NEXT P
310 COLOR= 15:HLIN 28,29 AT 7:HLIN 28,29 AT 10:PLOT 28,8:PLOT 29,9:HLIN 31,33 AT 7:VLIN 8,10 AT 32:VLIN 8,9 AT 34:VLIN 8,9 AT 36:PLOT 35,7:PLOT 35,10
320 VLIN 7,10 AT 38:VLIN 7,8 AT 39
330 X=20
340 AG=1:REM  CAR DIRECTION
350 A= RND (13)+11
360 REM  SEE IF CAR COMPLETED THE LAPS
370 B=0:S=S-1:IF S=0 AND L=4 THEN 950:IF S=0 THEN 910
380 REM  TRAFFIC LIGHT
390 COLOR= 9:HLIN 2,6 AT 39:HLIN 3,5 AT 1:HLIN 3,5 AT 38
400 VLIN 2,39 AT 4:FOR P=2 TO 6:VLIN 2,15 AT P:NEXT P
410 T= RND (3):COLOR= 0
420 IF T=0 THEN COLOR= 1:FOR P=3 TO 5:VLIN 3,5 AT P:NEXT P:COLOR= 0
430 IF T=1 THEN COLOR= 13:FOR P=3 TO 5:VLIN 7,9 AT P:NEXT P:COLOR= 0
440 IF T=2 THEN COLOR= 12:FOR P=3 TO 5:VLIN 11,13 AT P:NEXT P
450 REM  MAKE ROAD
460 COLOR= 15:VLIN 0,39 AT 9:VLIN 0,39 AT 26
470 REM  IF BUTTON DOWN THEN RUN
480 IF  PEEK (-16287)>127 THEN E=5
490 REM  FOR-NEXT MAKES MAN RUN
500 FOR U=1 TO E
510 X1=PDL (0)
520 REM  ERASE THE OLD MAN, LATER DRAW A NEW ONE
530 FOR W=0 TO 50:NEXT W:COLOR= 0:VLIN Y,Y+2 AT X:HLIN X-1,X+1 AT Y+1:VLIN Y+3,Y+4 AT X+1:VLIN Y+3,Y+4 AT X-1
540 REM  MAKE MAN MOVE IN THE PROPER DIRECTION
550 IF X1<50 THEN X=X-1
560 IF X1>150 THEN X=X+1
570 REM  MAKE MAN MOVE UPWARD
580 Y=Y-2
590 REM  DRAW NEW MAN
600 COLOR= 13
610 REM  MAKE SURE MAN DOESN'T GIVE A RANGE ERROR
620 IF X-1<10 THEN X=11:IF Y<0 THEN Y=35:IF Y>35 THEN Y=0:IF X+1>25 THEN X=24
630 REM  DRAW NEW MAN
640 VLIN Y,Y+2 AT X:HLIN X-1,X+1 AT Y+1:VLIN Y+3,Y+4 AT X+1:VLIN Y+3,Y+4 AT X-1:P= PEEK (-16336)+ PEEK (-16336)+ PEEK (-16336)+ PEEK (-16336)
650 REM  SEE IF MAN CRASHED
660 IF X+1>=A-1 AND X-1<=A+1 AND (Y+4<B+6 OR Y<B+6) AND (Y>B-1 OR Y+4>B-1) THEN 810
670 NEXT U
680 FOR G=1 TO E
690 REM  ERASE OLD CAR
700 FOR W=0 TO 20:NEXT W:COLOR= 0:VLIN B,B+5 AT A:PLOT A-1,B:PLOT A+1,B:PLOT A-1,B+5:PLOT A+1,B+5
710 IF B-5>Y+5 THEN 370
720 REM  MOVE CAR
730 B=B+2:A=A+AG:IF A= RND (13)+11 OR A=24 OR A=11 THEN AG=-AG:IF B+5>39 THEN 370
740 REM  DRAW NEW CAR
750 COLOR= 2
760 VLIN B,B+5 AT A:PLOT A-1,B:PLOT A+1,B:PLOT A-1,B+5:PLOT A+1,B+5
770 IF X+1>=A-1 AND X-1<=A+1 AND (Y+4<B+6 OR Y<B+6) AND (Y>B-1 OR Y+4>B-1) THEN 810
780 NEXT G:E=0
790 GOTO 410
800 REM  DRAW FLAME
810 COLOR= 2:HLIN A,A+2 AT B:VLIN B,B+5 AT A+1:HLIN A,A+2 AT B+5
820 D=Y
830 COLOR= 9:FOR A=A+1 TO A+10:B=B+1:D=D+1:VLIN B,D+5 AT A:IF D+5>34 THEN A=A+10:NEXT A
840 COLOR= 13:VLIN Y,Y+2 AT X:HLIN X-1,X+1 AT Y+1:VLIN Y+3,Y+4 AT X+1:VLIN Y+3,Y+4 AT X-1
850 REM  MAKE LIFE PASS BEFORE YOUR EYES
860 FOR Q=0 TO 230:P= PEEK (-16336):NEXT Q:FOR Q=0 TO 50:NEXT Q:GR :FOR Q=0 TO 15:COLOR= Q:FOR W=0 TO 39:VLIN 0,39 AT W:NEXT W,Q
870 PRINT "YOUR LIFE JUST PASSED BEFORE YOUR EYES. IT SURE WAS COLORFULL!!!!!!!!!!!"
880 INPUT "DO YOU WANT TO TRY AGAIN AT A LOWER     SKILL LEVEL?",A$:IF A$="NO" THEN 960:IF A$="N" THEN 960
890 CALL -936
900 L=L-1:IF L=0 THEN L=1:S=L:GOTO 260:REM  MAKE A LOWER SKILL LEVEL
910 PRINT "YOU LUCKY WINNER!!!!!<CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G> YOU DIDN'T EVEN   HAVE YOUR LIFE INSURANCE PAID UP!!!"
920 INPUT "DO YOU WANT TO PLAY AGAIN AT A HIGHER   SKILL LEVEL?",A$:IF A$="N" THEN 960:IF A$="NO" THEN 960
930 CALL -936
940 L=L+1:IF L=5 THEN L=4:S=L:GOTO 260:REM  MAKE HIGHER SKILL LEVEL
950 PRINT "YOU MADE IT PAST PRO! NOW YOU CAN HANDLEFREE WAY CROSSINGS!!!!!!"
960 TEXT :CALL -936:END 

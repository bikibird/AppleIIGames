10  POKE 2,173: POKE 3,48: POKE 4,192: POKE 5,136: POKE 6,208: POKE 7,4: POKE 8,198: POKE 9,1: POKE 10,240
20  POKE 11,8: POKE 12,202: POKE 13,208: POKE 14,246: POKE 15,166: POKE 16,0: POKE 17,76: POKE 18,2: POKE 19,0: POKE 20,96
30 DIM A$(20):DIM B$(20):DIM C$(20):TEXT 
40 CALL -936:VTAB 10:TAB 15:PRINT "BASKETBALL":VTAB 12:TAB 19:PRINT "BY":VTAB 14:TAB 14:PRINT "JENNY SCHMIDT":FOR W=0 TO 3000:NEXT W
50 CALL -936:VTAB 7:PRINT "THE OBJECT OF THE GAME IS TO GET THE    BASKETBALL INTO YOUR BASKETBALL HOOP."
60 PRINT :PRINT :PRINT "THIS IS DONE USE THE PADDLE CONTROLS.   PDL(0) CONTROLS THE RED PADDLE. PDL(1)   CONTROLS THE BLUE PADDLE."
70 PRINT :PRINT :PRINT "GOOD LUCK TO BOTH TEAMS"
80 PRINT :PRINT :TAB 7:PRINT "(NO MORE THAN 10 CHARACTERS)"
90 INPUT "FIRST TEAM NAME? ",A$:PRINT :PRINT :PRINT :INPUT "SECOND TEAM NAME? ",B$
100 PRINT :PRINT :INPUT "HOW LONG OF A GAME (2-15 MIN.)",WI:IF WI<2 OR WI>15 THEN 100:WI=WI*460
110 GR 
120 CALL -936:PRINT A$;": ";:TAB 25:PRINT B$;": ";
130 T=1:A=20:B=20:DA=T:DB=0
140 COLOR= 1:HLIN 34,39 AT 15:VLIN 11,15 AT 34:VLIN 10,15 AT 39
150 COLOR= 2:HLIN 0,5 AT 15:VLIN 10,15 AT 0:VLIN 11,15 AT 5
160 X=PDL (0)/7:Y=PDL (1)/7
170 VTAB 21:TAB 14:PRINT "TIME ";(WI-D)/460;":";:IF ((WI-D) MOD 460)/8<10 THEN PRINT "0";((WI-D) MOD 460)/8;
180 IF ((WI-D) MOD 460)/8>9 THEN PRINT ((WI-D) MOD 460)/8;
190 A1=A:B1=B:COLOR= 14:PLOT A1,B1
200 COLOR= 1:VLIN X,X+3 AT 17:COLOR= 2:VLIN Y,Y+3 AT 23
210 A=A+DA:B=B+DB
220 IF A>33 AND B<16 AND B>9 THEN GOSUB 310
230 IF A<6 AND B<16 AND B>9 THEN GOSUB 450
240 IF B<1 OR B>38 THEN GOSUB 670
250 IF A<1 OR A>38 THEN GOSUB 690
260 IF A>15 AND A<19 AND B>X-1 AND B<X+4 THEN GOSUB 590
270 IF A>21 AND A<25 AND B>Y-1 AND B<Y+4 THEN GOSUB 630
280 COLOR= 0:PLOT A1,B1:VLIN X,X+3 AT 17:VLIN Y,Y+3 AT 23
290 D=D+1:IF D=WI THEN 710
300 GOTO 160
310 IF A=39 AND DB>0 THEN 330
320 IF  SCRN(A,B)>0 OR  SCRN(A-SGN (DA),B) OR  SCRN(A,B-SGN (DB))>0 THEN 430
330 COLOR= 0:PLOT A1,B1
340 FOR W=255 TO 30 STEP -40
350 COLOR=  RND (15)+1:PLOT 37,12
360  POKE 0,W: POKE 1,30:CALL 2
370 NEXT W
380 COLOR= 0:PLOT 37,12
390 SX=SX+2:VTAB 21:TAB  LEN(A$)+2:PRINT SX;:A=20:B=20:DA=T:DB=0:T=-T
400  POKE 50,127:VTAB 23:TAB 8:PRINT "HIT SPACE BAR TO SERVE BALL";:CALL -756
410  POKE 50,255:VTAB 23:TAB 8:PRINT "                            "
420 RETURN 
430 DA=-DA:DB=-DB:A=A+DA:B=B+DB
440  POKE 0,100: POKE 1,4:CALL 2:RETURN 
450 IF A=0 AND DB>0 THEN 470
460 IF  SCRN(A,B)>0 OR  SCRN(A-SGN (DA),B) OR  SCRN(A,B-SGN (DB))>0 THEN 570
470 COLOR= 0:PLOT A1,B1
480 FOR W=255 TO 30 STEP -40
490 COLOR=  RND (15)+1:PLOT 3,12
500  POKE 0,W: POKE 1,30:CALL 2
510 NEXT W
520 COLOR= 0:PLOT 3,12
530 SY=SY+2:VTAB 21:TAB  LEN(B$)+27:PRINT SY;:A=20:B=20:DA=T:DB=0:T=-T
540  POKE 50,127:VTAB 23:TAB 8:PRINT "HIT SPACE BAR TO SERVE BALL";:CALL -756
550  POKE 50,255:VTAB 23:TAB 8:PRINT "                            "
560 RETURN 
570 DA=-DA:DB=-DB:A=A+DA:B=B+DB
580  POKE 0,100: POKE 1,4:CALL 2:RETURN 
590 IF B=X THEN DB=-2:IF B=X+1 THEN DB=-1:IF B=X+2 THEN DB=1:IF B=X+3 THEN DB=2:DA=-DA
600 COLOR= 0:PLOT 17,B1
610  POKE 0,255: POKE 1,4:CALL 2
620 IF SGN (DA)=-1 THEN A=15:IF SGN (DA)=1 THEN A=19:RETURN 
630 IF B=Y THEN DB=-2:IF B=Y+1 THEN DB=-1:IF B=Y+2 THEN DB=1:IF B=Y+3 THEN DB=2:DA=-DA
640 COLOR= 0:PLOT 23,B1
650  POKE 0,255: POKE 1,4:CALL 2
660 IF SGN (DA)=-1 THEN A=21:IF SGN (DA)=1 THEN A=25:RETURN 
670  POKE 0,50: POKE 1,4:CALL 2
680 IF B<0 THEN B=0:IF B>39 THEN B=39:DB=-DB:RETURN 
690  POKE 0,50: POKE 1,4:CALL 2
700 IF A<0 THEN A=0:IF A>39 THEN A=39:DA=-DA:RETURN 
710 VTAB 22:TAB 1:IF SX>SY THEN PRINT "CONGRATULATIONS, ";A$;", YOU WON"
720 IF SX<SY THEN PRINT "CONGRATULATIONS, ";B$;", YOU WON"
730 IF SX=SY THEN PRINT "ITS A TIE"
740 INPUT "DO YOU WANT TO PLAY AGAIN? ",C$:IF C$="NO" OR C$="N" THEN 2550
750 GOTO 770
760 TEXT :CALL -936:END 
770 CALL -936:PRINT A$;": ";:TAB 25:PRINT B$;": ";
780 SX=0:SY=0
790 D=0:GOTO 100
2250 END 

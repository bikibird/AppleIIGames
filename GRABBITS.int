10  POKE 2,173: POKE 3,48: POKE 4,192: POKE 5,136: POKE 6,208: POKE 7,4: POKE 8,198: POKE 9,1: POKE 10,240
20  POKE 11,8: POKE 12,202: POKE 13,208: POKE 14,246: POKE 15,166: POKE 16,0: POKE 17,76: POKE 18,2: POKE 19,0: POKE 20,96
30 DIM A$(20)
40 TEXT :CALL -936:VTAB 10
50 TAB 16:PRINT "GRABBITS"
60 PRINT :TAB 19:PRINT "BY"
70 PRINT :TAB 14:PRINT "JENNY SCHMIDT"
80 FOR I=0 TO 3000:NEXT I
90 PRINT "TODAY YOU GET TO GO GRABBIT HUNTING.    YOU'LL GET A GRABBIT BAG THAT LOOKS     LIKE THIS:"
100 TAB 15:PRINT "#####"
110 FOR I=1 TO 3:TAB 15:PRINT "#":NEXT I
120 TAB 15:PRINT "#####"
130 PRINT :PRINT "YOU'LL HUNT GRABBITS THAT LOOK LIKE     THIS:   @"
140 TAB 8:PRINT "@":TAB 9:PRINT "@"
150 PRINT "YOU MOVE THE BAG WITH THE ZERO PADDLE.  EACH TIME YOU CACTH A GRABBIT YOUR BAG  GETS SMALLER. EACH TIME YOU ";
160 PRINT "MISS THE BAGGETS LARGER AND YOU MOVE FORWARD.IF YOURBAG CLOSES YOU WIN. IF IT FALLS OFF THE EDGE YOU LOSE."
170 PRINT :PRINT "NOTE: THE GRABBIT MUST ENTER THROUGH    THE FRONT OF THE BAG."
180 PRINT 
190 TAB 15:PRINT "HIT ANY KEY TO START";:CALL -756:CALL -936
200 REM  X,Y,S,G DRAW GRABBIT BAG---S IS USED TO MAKE BAG GET LARGER AND SMALLER
210 GR :Y=1
220 S=6:G=3
230 Y=Y+5:S=S+1
240 REM  A,B DRAW GRABBITS
250 B= RND (38)+1:A=39
260 REM  DRAW BOARDER
270 COLOR= 2:VLIN 0,39 AT 0:HLIN 0,35 AT 0:HLIN 0,35 AT 39
280 REM   DRAW GRABBIT BAG
290 COLOR= 13
300 X=PDL (0)/8
310 IF S=1 THEN 570
320 IF Y+3>39 THEN 580
330 IF PDL (0)=255 THEN X=38-S
340 VLIN X,X+S AT Y
350 HLIN Y,Y+G AT X
360 HLIN Y,Y+G AT X+S
370 IF Y+G>37 THEN 580
380 REM D CHANGES THE GRABBIT'S DIRECTION 
390 D= RND (3):IF D=0 OR D=3 THEN D= RND (-3)-1
400 REM  DRAW GRABBIT
410 B=B+D:A=A-3
420 IF B-1<1 THEN B=2:IF B+1>38 THEN B=37
430 COLOR= 1
440 PLOT A,B:PLOT A+1,B-1:PLOT A+1,B+1
450 REM  MAKE TONE
460 P=B*3:D=10: POKE 0,P: POKE 1,D:CALL 2
470 REM  ERASE
480 FOR W=0 TO 30:NEXT W:COLOR= 0:PLOT A,B:PLOT A+1,B-1:PLOT A+1,B+1:VLIN X,X+S AT Y:HLIN Y,Y+G AT X:HLIN Y,Y+G AT X+S
490 REM  CHECK IF GRABBIT IS IN THE BAG
500 IF (B<=X+S AND B>=X) AND (A<=Y+G AND A>Y) THEN 560
510 REM  IF GRABBIT SNEAKS BY MOVE,INCREASE, AN START OVER
520 IF A<Y THEN 230
530 REM   IF GRABBIT ISN'T THERE YET, CONTINUE
540 GOTO 270
550 REM   IF YOU CATCH THE GRABBIT SHRINK AND START OVER
560 S=S-1:GOTO 250
570 PRINT "YOU WIN":TEXT :GOTO 590
580 PRINT "YOU LOSE":INPUT "DO YOU WANT TO TRY AGAIN?",A$:CALL -936:IF A$="YES" OR A$="Y" THEN 210:TEXT :CALL -936:END 
590 PRINT :PRINT 
600 TAB 17:PRINT "*RECIPE*"
610 PRINT :PRINT :TAB 15:PRINT "GRABBIT STEW"
620 PRINT :TAB 12:PRINT "2 POUNDS DICED GRABBIT MEAT"
630 PRINT :TAB 12:PRINT "2 CUPS SLICED CARROTS":PRINT :TAB 12:PRINT "4 STALKS OF CELERY, CHOPPED"
640 PRINT :TAB 12:PRINT "2 TBS. SALT":PRINT :TAB 12:PRINT "8 QUARTS OF WATER"
650 PRINT :PRINT :PRINT "MIX ALL INGREDIANTS INTO A LARGE VAT.   COOK OVER A ROARING FIRE."
660 INPUT "DO YOU WANT TO PLAY AGAIN?",C$:CALL -936:IF C$="Y" OR C$="YES" THEN 210
670 TEXT :CALL -936:END 
